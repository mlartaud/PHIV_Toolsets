// PHIV NDPI Tool Set
// Marc LARTAUD MRI PHIV
// Boite a outils pour faciliter l export des fichiers ndpi issu du nanozoomer

// installer loci bio formats
//http://www.loci.wisc.edu/files/software/loci_tools.jar



/*
Outil Ndpi : Pour previsualuser le contenu d un fichier Ndpi a faible resolution (serie4)

Outi CropLoci : Pour extraire au format tiff et a la resolution choisie (serie de 1 a 3)
	une zone selectionnee sur la previsualisation via l outil Ndpi
	Option clic droit pour le choix de la resolution

Outil NdpiMacroBatch Permet de decouper et exporter au format tiff les fichiers ndpi d un repertoire source
	dans un repertoire cible 
	avec l option N° de serie allant de   1 à 3 de la plus forte a la plus petite resolution 
	Crop Automatique pour decouper les objets detectes sinon exporte toute l image  
	Filtre gaussien pour lisser les objets
	Taille mini des objets en cm²
	Elargissement en pixel pour decouper plus grand que l objet

 

Outil Renommer permet de renommer tous les fichiers d un repertoire
	il faut choisir le repertoire
	copier la liste des noms dans excel
	ecrire les nouveaux noms dans la colonnes voisine correspondante
	copier dans le presse papier les 2 colonnes
	puis cliquer sur ok

*/

//variables glogales
var path="";
var FacteurMultiplicatif=0;
var NomImage="nom d image";
var numserie=2;
var numserie_crop=2;
//crop
var taille_crop=10; // mini en cm²
var filtre_crop=5;
var enlarge_crop=5;
var SX=newArray (6);
var SY=newArray (6);
var autocrop=true;


macro "AutoRun"{
	setTool("rectangle");
	setForegroundColor(255, 255, 255);//premier plan blanc
	setBackgroundColor(0,0,0);// fond noir
	run("Set Measurements...", "area shape display redirect=None decimal=3");
	requires("1.49p");
}


////////////////////////////////////////////////////////////////label=Ndpi////////////////////////////////////////////
macro "Ndpi Action Tool - C778D5eDcdCeefDe7Cc7fD59D5aDa4Db5CfffD00D10D12D15D16D17D18D19D1aD1bD1cD1dD20D24D25D26D27D28D29D2aD2bD2cD2dD34D35D36D37D38D39D3aD3bD3cD3dD41D44D45D46D47D4dD51D54D55D5dD61D71D77D78D79D7aD7fD81D85D86D87D88D89D8aD8fD91D95D96D97D98D99D9cD9fDa1Da2Da6Da7Da8DabDacDafDb1Db2DbbDbcDbfDc1Dc2Dc3DcaDcbDcfDd1Dd2Dd3Dd4Dd5Dd6Dd7Dd8Dd9DdaDdbDdeDdfDeeDefDfeDffCddeD7dDdcDe3CfffD48D6dDccCdafD4cD57D6bDb8Dc8CaaaD8eDedDf2CfefD56D9aDa9Cc8fD67Db9CeeeD02D03D04D05D06D07D08D09D0aD0bD0cD0dD5fDbeDd0De4De5De6De8De9DeaDebCfffD14D22D64D6aDc9CecfD92CaaaD1fDf3DfcCfffD30D31D6fDceCc8fD5cD72D73Da3Dc7CeeeD11D21D50D70D80D90Dc0CdbfD49D68CcccD3fCcafD6cDc5CfefD69D8cDb3DbaDc4C999Df4Df5Df6Df7Df8Df9DfaDfbCc8fD4bD52D58D5bD62D66D74D83D93Db4Dc6CdeeD40D60Db0CdbfD65D9bDa5Db7CbbbD2eCc9fD42D4aCedfD43D53D76C99aD3eCecfD13D32D33D63D7bD84CdddDe1De2CdafD23D82D8bD94DaaC889D4eCbbbD2fDf1Cc9fD75Db6CebfD7cCcddD1eCdeeDa0CbbcD0eCaaaD9dCdddD4fDaeDf0DfdC888D6eCaabDecCccdDe0CbbcD8dC999DadCdddD0fCcccD9eC999D7eDddC788DbdCccdD01"{

path = File.openDialog("Choose a file");
pathsansext=substring(path,0,indexOf(path,".ndpi"));
NomImage=File.getName(path);
Bio_meta(path);
run("Bio-Formats Importer", "open=["+path+"] color_mode=Default view=Hyperstack stack_order=XYCZT series_4");
Hyper2RGB();
//Array.show("title", SX, SY);
}

////////////////////////////////////////////////////////////////label=CropLoci////////////////////////////////////////////

macro "CropLoci Action Tool - C000D0fDedCdddD10D36D37D38D39D4dD5dD6dD7dD8dD9dDadDbdDe5C777DcaDf2CeeeD0cD21D31D41D51D61D71D81D91Da1Db1Dc1Dd1De0C444DffCdddD2fD32D3eD3fD42D46D47D48D49D4eD4fD52D56D57D58D59D5eD5fD62D66D67D68D69D6eD6fD72D76D77D78D79D7eD7fD82D86D87D88D89D8eD8fD92D96D97D98D99D9eD9fDa2Da6Da7Da8Da9DaeDafDb2Db6Db7Db8Db9DbeDbfDc2DceDcfDdfDe6De7De8De9DeaDf4Df5Df6Df7Df8Df9DfaDfbDfcCbbbD0dD2eD33D3aD43D53D63D73D83D93Da3Db3DdeDf3CeeeD04D05D06D07D08D09D0aD0bD20D30D40D50D60D70D80D90Da0Db0Dc0Dd0C333D01D34D44D54D64D74D84D94Da4Db4Dd5Dd6Dd7Dd8Dd9DdaDf1CaaaDe1De4DefDfdC666D23D2dDc5CcccD00D03D4aD5aD6aD7aD8aD9aDaaDbaC333D1eD25D26D27D28D29D2aD3bD4bD5bD6bD7bD8bD9bDabDbbDc4DeeC888D02D1fDddC666D4cD5cD6cD7cD8cD9cDacDbcDccCbbbD22D35D45D55D65D75D85D95Da5Db5Dc3Dd2CcccD3dDcdDebDf0C222D1dDd4C888D11D14Dc6Dc7Dc8Dc9C555D0eD3cDd3C999D15D16D17D18D19D1aD1bC111D12D2bD2cDcbDdcDe2C777D1cDecC444DfeC222D13DdbDe3C111D24"{


//waitForUser("Faire une Selection");
getSelectionBounds(x, y, width, height);

for (s=1;s<4;s++){
if (s==numserie_crop) FacteurMultiplicatif = SX[s-1]/SX[3];
}

x=(FacteurMultiplicatif)*x;
y=(FacteurMultiplicatif)*y;
width=(FacteurMultiplicatif)*width;
height=(FacteurMultiplicatif)*height;
if (width*height < 20000*20000){
	run("Bio-Formats Importer", "open=["+path+"] color_mode=Default crop view=Hyperstack stack_order=XYCZT series_"+numserie_crop+" x_coordinate_"+numserie_crop+"="+x+" y_coordinate_"+numserie_crop+"="+y+" width_"+numserie_crop+"="+width+" height_"+numserie_crop+"="+height+"");
	Hyper2RGB();
	}
	else print ("Taille trop grande");
}


macro 'CropLoci Action Tool Options'  {
    //on right-click, the corresponding 'Options' macro is run
    Dialog.create("Serie"); //e.g., put a dialog here
	Dialog.addSlider("Numero de serie:", 1, 3, numserie_crop);
    Dialog.show();
    numserie_crop = Dialog.getNumber();
  	}



////////////////////////////////////////////////////////////////label=NdpiExportBatch////////////////////////////////////////////

macro "NdpiMacroBatch Action Tool - C2a3D47CbceD89CbbcDaaCfffD00D01D02D03D04D05D06D07D09D0aD0bD0cD0dD0eD0fD10D11D12D13D1cD1dD1eD1fD20D21D2dD2eD2fD30D3eD3fD40D4eD4fD50D5fD60D6fD7fD8fD9fDa0DafDb0DbeDbfDc0Dc1DceDcfDd0Dd1Dd2DddDdfDe0De1De2DeaDebDedDefDf0Df1Df2Df3Df4Df5Df6Df7Df8Df9DfaDfbDfcDfdDfeDffCa99Dc6CffeDc8CcccD81C877D7cCeeeD4dD61DcdDd3DdcDe7CbbdD7aCbbbD7bDd4CfffD3dCcceD93C3c1D46CeeeD8eCcbcD6dCaaaD95Dc4CfffDd8De3De9CccdD68C998D9cCeefD18Da1DdaCbbdD8aC9d9D25CfffD08DaeDd9De8CddeD3aD3cD5aD7eC876D96Da6CeeeD15D23CbbbD8dCaaaDcbDd6CfffD14D1bD22D2cD41D70D80D90D9eDc2CcecD24C888Dc5CcccD2aDa2CacaD62CfffD5eDb1DdeDeeCdddD28C6b5D55CeeeD6eCbcbD58CaabDb4CdddD16C7a7D63CeffD17D51CccdDb9CbcbD76CeeeDb2Db8DdbDe4C4a4D37CeeeD19D29D39D4aDc9CabcD94D9aCaa9D97CccdD2bD49D92C887D8cCbbdD6aCbbbDbcCfffD31DecCdceD4bC4c2D45CbbcD59D74Da8CaabDbaCdddD91C998Db5CbcdD84CbcaD33C6a6D66CcbbD9dCba9Da7CdddD5dD75D82Dd7De6C988D86CcccDc3CacaD38D52D67CdedD32C6b6D34CeefD5bCbcbD72CbabD7dCdddD1aC8c6D43D54CccdD83D88CbdbD42C3a3D56CcceD69CbbcD98Db3Caa9D87CcccDadC777DbbCbbdDa9CabbD73CcceD78C3b2D36C988D9bCbbbDccC888DabCbbbD77CdddD71DbdC6d4D44CcccDc7CaabDa4C8a8D48CccdDa3C6a6D26Caa9D8bC987Db6CcbdD4cD5cCbbbDcaCddeD6bC4c3D35CbbaDb7Ca99Da5CbbeD99CadaD27C6b6D57CbbcD3bD6cCaaaDacC888Dd5C7b6D53CcdbD64CabbD85CcdbD65CccfD79CddcDe5"{

Dialog.create("NdpiMacroBatch"); //e.g., put a dialog here
	Dialog.addSlider("Numero de serie pour les ndpi:", 1, 3, numserie);
	Dialog.addCheckbox("Crop Automatique", autocrop); 
	Dialog.addNumber("Filtre Gaussien :", filtre_crop);
    Dialog.addNumber("Taille mini (cm²) :", taille_crop);
	Dialog.addNumber("Elargissement :", enlarge_crop);
	Dialog.show();
	numserie = Dialog.getNumber();
	autocrop=Dialog.getCheckbox();
    filtre_crop = Dialog.getNumber();
	taille_crop = Dialog.getNumber();
	enlarge_crop = Dialog.getNumber();
	
	
dir1 = getDirectory("Choix du Repertoire Source ");
list = getFileList(dir1);
dir2 = getDirectory("Choix du Repertoire Cible ");	
Vide_Roi();
      for (i=0; i<list.length; i++) {
          showProgress(i+1, list.length);
			path=dir1+list[i];
			nom=list[i];
		if (autocrop){
           if (endsWith(list[i], '.ndpi')){
			nom=substring(nom,0,indexOf(nom,".ndpi"));
			nom=replace(nom, " ", "");
			Bio_meta(path);
			run("Bio-Formats Importer", "open=["+path+"] color_mode=Default view=Hyperstack stack_order=XYCZT series_4");
			Hyper2RGB();
			origid=getImageID();
			Detect_Roi(filtre_crop,taille_crop);
			count=roiManager("count");
			for(j=1;j<=count;j++){
				selectImage(origid);
				roiManager("Select", j-1);
				run("Enlarge...", "enlarge="+enlarge_crop+" pixel");
				getSelectionBounds(x, y, width, height);
				for (s=1;s<4;s++){
				if (s==numserie) FacteurMultiplicatif = SX[s-1]/SX[3];
				}
				x=(FacteurMultiplicatif)*x;
				y=(FacteurMultiplicatif)*y;
				width=(FacteurMultiplicatif)*width;
				height=(FacteurMultiplicatif)*height;
				if (width*height < 20000*20000){
					run("Bio-Formats Importer", "open=["+path+"] color_mode=Default crop view=Hyperstack stack_order=XYCZT series_"+numserie+" x_coordinate_"+numserie+"="+x+" y_coordinate_"+numserie+"="+y+" width_"+numserie+"="+width+" height_"+numserie+"="+height+"");
					Hyper2RGB();
					}
					else print ("Taille trop grande");	
				saveAs("Tiff",dir2+nom+"-"+j);	
				close();			
				}
			selectImage(origid);
			close();
			Vide_Roi();
		   }
		   if (endsWith(list[i], '.tif')){
			nom=substring(nom,0,indexOf(nom,".tif"));
			nom=replace(nom, " ", "");
			open(path);
			origid=getImageID();
			Detect_Roi(filtre_crop,taille_crop);
			count=roiManager("count");
			for(j=1;j<=count;j++){
				selectImage(origid);
				roiManager("Select", j-1);
				run("Enlarge...", "enlarge="+enlarge_crop+" pixel");
				run("Duplicate...", "title="+nom+"-"+j);
				run("Select None");
				saveAs("Tiff",dir2+nom+"-"+j);	
				close();				
				}
			close();
			Vide_Roi();
		   }
		   }
		   else {
		   nom=substring(nom,0,indexOf(nom,".ndpi"));
			nom=replace(nom, " ", "");
			Bio_meta(path);
			for (s=2;s<4;s++){
					if (s==numserie)  Max=SX[s-1]*SY[s-1];
				}					   
			 if (Max < 20000*20000){
			   run("Bio-Formats Importer", "open=["+path+"] color_mode=Default crop view=Hyperstack stack_order=XYCZT series_"+numserie);
			   Hyper2RGB();
			   saveAs("Tiff", dir2+nom+".tif");
			   close ();
			}
			else print (nom+" taille trop grande");
		   		   
		   }
		   
	   }

	}
	/*
macro 'NdpiMacroBatch Action Tool Options'  {
	Dialog.create("NdpiMacroBatch"); //e.g., put a dialog here
	Dialog.addNumber("Filtre Gaussien :", filtre_crop);
    Dialog.addNumber("Taille mini (cm²) :", taille_crop);
	Dialog.addNumber("Elargissement :", enlarge_crop);
	Dialog.addSlider("Numero de serie pour les ndpi:", 1, 3, numserie);
    Dialog.show();
    filtre_crop = Dialog.getNumber();
	taille_crop = Dialog.getNumber();
	enlarge_crop = Dialog.getNumber();
	numserie = Dialog.getNumber();
    
  	}
*/

macro "Renommer Action Tool - C333D37D47D57D67D80D9eDa1Db1Db4DbaDbdDceDd0DdeCcccD0bD19D26D85D8dDc2C666D84D8aDa3Da4Da5Da6Da7Da8Da9DaaDabDd1CfffD00D01D02D0cD0dD0eD0fD10D11D12D1cD1dD1eD1fD20D21D22D2cD2dD2eD2fD30D31D32D3cD3dD3eD3fD40D41D42D4cD4dD4eD4fD50D51D52D5cD5dD5eD5fD60D61D62D6cD6dD6eD6fD72D7cD82D86D8cDd6Dd7Dd8DdcDe2DecDf2DfcC444DaeDeaCeeeD13D23D33D43D53D63D73D95D9bDd3De3Df1DffC999D15D28D36D46D56D66Db0C444D90DadDb3Db5Db6Db7Db8Db9DbcDc1CeeeD29D79D7dD92DbfDf0DfeC777D78DcdDfaC666D04D77DacCfffD03D83D87D88D89D93D96D97D98D99D9cDd2Dd9Df3DfdCbbbD25D35D45D55D65D75D7fD8bDd5DddC333D05D06D07D08D09D0aD8eDb2DbbCdddD39D49D59D69D94D9aDc6Dc7Dc8DccC777D81De5Df5Df6Df7Df8Df9C555D14D24D2aD34D3aD44D4aD54D5aD64D6aD74D7aDa2Dd4DdaDe4CbbbD1bD2bD3bD4bD5bD6bD71D76D7bDcfDdbDebC999D9dDe1De6De7De8Df4CdddDafDc3Dc9DedDfbC555D1aD38D48D58D68D91DbeDc0Dc4DcaDe0DeeCaaaD9fDc5DcbDefC888D8fDdfDe9CcccD16D17D18C888D27D70D7eDa0"{
	rename_files();
}

macro "jpeg2000 Action Tool - C000D3cD4aD4bD4dD58D5bD6bD6dD78D79D7cD88D8bD98Da1Da2Da3Db4Dc5Dc6Dc7Dc8Dc9DcaDcbDccDcdDdeCfffD00D01D02D03D04D05D06D07D08D09D0aD0bD0cD0dD0eD0fD10D11D12D13D14D15D16D17D18D19D1aD1bD1cD1dD1eD1fD20D21D22D23D24D25D26D27D28D29D2aD2bD2cD2dD2eD30D31D32D33D34D35D36D37D38D39D3aD3dD40D41D42D43D44D45D46D47D50D51D52D53D54D55D56D60D61D62D63D64D70D71D72D7dD80D89D8cD8dD99D9bD9cDb0Db1Db2Db3Dc0Dc1Dc2Dc3Dc4Dd0Dd1Dd2Dd3Dd4Dd5Dd6Dd7Dd8Dd9DdaDdbDdcDddDe0De1De2De3De4De5De6De7De8De9DeaDebDecDedDeeDefDf0Df1Df2Df3Df4Df5Df6Df7Df8Df9DfaDfbDfcDfdDfeDffCc34D5cD67D74DceDdfCb24D3fD49D4cD4fD59D5aD5fD66D6aD6cD6fD7aD7fD82D85D86D8fD90D94D95D96D97D9fDa4Da5Da6Da7Da8Da9DaaDabDacDadDafDb5Db6Db7Db8Db9DbaDbbDbcDbdDbfDcfCe84D2fC333D77D92D93Ce74D3bD75D83D9dCfa4D81Ce64D68D8aD9aCf94D57D87Da0Ca84D3eD7bCfb4D65D73Ce84D5dC984D48D4eD5eD69D6eD76D7eD84D8eD91D9eDaeDbe"{


dir1 = getDirectory("Choix du Repertoire Source ");
list = getFileList(dir1);
dir2 = getDirectory("Choix du Repertoire Cible ");	
dirmacro=getDirectory("macros");
dirjp2=dirmacro+"bin\\jp2\\";
print("\\Clear"); 
for (i=0; i<list.length; i++) {
          showProgress(i+1, list.length);
			
			//pathsansext=substring(path,0,indexOf(path,".czi"));
			nom=substring(list[i],0,indexOf(list[i],"."));
			
			command=dirjp2+"JP2WSIConverter -i "+dir1+list[i]+" -o "+dir2+nom+".jp2"+" -c "+30;
			print (command);	
	}


waitForUser("Conversion via JP2WSIConverter", "Fermer les fenetres de commande une fois la conversion terminee");
selectWindow("Log");//sauvegarder en .bat
		saveAs("Text",dirjp2+"cmd.txt");
		if (File.exists(dirjp2+"cmd.bat")) File.delete(dirjp2+"cmd.bat");
        File.rename(dirjp2+"cmd.txt",dirjp2+"cmd.bat");
		exec("cmd", "/c", "start", dirjp2+"cmd.bat");
		

print("ok");

exit();

}




macro "About These Macros Action Tool - C000C111C222C333C444C555D54D55D63D73D7cD7dD83D88D89D8aD8cD8dD93D97Da3Da4Da5Da6C555D7aDb5C555D72D82C555D45Da7C555D98C555D53C555D79C555C666Db4C666Db6C666C777D92C777D87C777D64C777D62C777C888D44C888D96C888D78C999D94C999CaaaCbbbD99Db3CbbbDa2CbbbDa8CbbbCcccDb7CcccD52CcccD43CcccD65CcccCdddD74CdddD77CdddD86CdddD84D95CdddD00D01D02D03D04D05D06D07D08D09D0aD0bD0cD0dD0eD0fD10D11D12D13D14D15D16D17D18D19D1aD1bD1cD1dD1eD1fD20D21D22D23D24D25D26D27D28D29D2aD2bD2cD2dD2eD2fD30D31D32D33D34D35D36D37D38D39D3aD3bD3cD3dD3eD3fD40D41D42D46D47D48D49D4aD4bD4cD4dD4eD4fD50D51D56D57D58D59D5aD5bD5cD5dD5eD5fD60D61D66D67D68D69D6aD6bD6cD6dD6eD6fD70D71D75D76D7bD7eD7fD80D81D85D8bD8eD8fD90D91D9aD9bD9cD9dD9eD9fDa0Da1Da9DaaDabDacDadDaeDafDb0Db1Db2Db8Db9DbaDbbDbcDbdDbeDbfDc0Dc1Dc2Dc3Dc4Dc5Dc6Dc7Dc8Dc9DcaDcbDccDcdDceDcfDd0Dd1Dd2Dd3Dd4Dd5Dd6Dd7Dd8Dd9DdaDdbDdcDddDdeDdfDe0De1De2De3De4De5De6De7De8De9DeaDebDecDedDeeDefDf0Df1Df2Df3Df4Df5Df6Df7Df8Df9DfaDfbDfcDfdDfeDff"{
open(getDirectory("macros")+"toolsets/PHIV_Scan_toolset.txt");
}

	

function rename_files(){

dir = getDirectory("Choisir un repertoire source");
list_old = getFileList(dir);
list_ext=newArray(list_old.length);
list_new=newArray(list_old.length);
for (i=0; i<list_old.length; i++) {
	list_ext[i]=substring(list_old[i],(lastIndexOf(list_old[i], ".")),lengthOf(list_old[i]));
	  }
String.resetBuffer;
 for (i=0; i<list_old.length; i++) {
		String.append(substring(list_old[i],0,lastIndexOf(list_old[i], ".")) + "\n");
	  }
  String.copy(String.buffer);
  waitForUser("Coller dans excel","La liste des fichiers est dans le presse papier\nColler la dans excel (Ctrl-v), \nEcrire les nouveaux noms\ndans la colonne voisine\nCopier les 2 colonnes dans le presse papier (Ctrl-c)\npuis cliquer sur OK");
  clipboard=String.paste;
  //String.show(liste);
  lines=split(clipboard, "\n");	
if (lengthOf(lines)<1)  exit;
for (i=0; i<lengthOf(lines); i++){
			row=split(lines[i],"\t");
			if (lengthOf(row)==2) list_new[i]=row[1];
			}
if (doublon(list_new)) exit("Il y a un doublon");
for (i=0; i<lengthOf(lines); i++){
	File.rename(dir+list_old[i],dir+"temp"+list_old[i]);
	}
for (i=0; i<lengthOf(lines); i++){
	print (list_old[i]+"-->"+list_new[i]);
	File.rename(dir+"temp"+list_old[i],dir+list_new[i]+list_ext[i]);
	}
	Array.show("titre",list_old,list_ext,list_new);
}

function doublon(Tableau){
	double=false;
	for (i=0;i<Tableau.length;i++){
		for (j=0;j<Tableau.length;j++){
		if ((Tableau[i]==Tableau[j])&&(i!=j)) double=true;		
		}
	 return double;
	}
}

function Hyper2RGB(){
	setBatchMode(true);
	titre=getTitle();
	setSlice(1);
	 setMetadata("Label", "r");
	setSlice(2);
	 setMetadata("Label", "g");
	setSlice(3);
	 setMetadata("Label", "b");
	run("Stack to Images");
	run("Merge Channels...", "c1=r c2=g c3=b");
	selectWindow("RGB");
	rename(titre);
	setBatchMode(false);
}

function Bio_meta(path){
	run("Bio-Formats Macro Extensions");
	Ext.setId(path);
	for (s=0; s<6; s++) {
	  Ext.setSeries(s);
	  Ext.getSizeX(sizeX);
	  SX[s]=sizeX;
	  Ext.getSizeY(sizeY);
	  SY[s]=sizeY;
	  }
	Ext.close();
}

function Vide_Roi(){
if (isOpen("ROI Manager")){
				roiManager("Deselect");
				n = roiManager("count");
				if (n>0) roiManager("Delete");
			}
}

function Detect_Roi(filtre_crop,taille_crop){
		run("Duplicate...", "title=temp");
		run("8-bit");
		run("Gaussian Blur...", "sigma="+filtre_crop+" scaled");
		setAutoThreshold("Default");	
		run("Analyze Particles...", "size="+(taille_crop*1000000)+"-Infinity include add");
		count=roiManager("count");
		for(i=0;i<count;i++){
				roiManager("Select", i);
				getSelectionBounds(x, y, width, height);
				x=10000+x;
				roiManager("Rename", ""+x);				
			}
		roiManager("Deselect");
		roiManager("Sort");
		close();

}


