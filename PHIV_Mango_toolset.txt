// PHIV Mango Tool Set
// Marc LARTAUD MRI PHIV
// Boite a outils pour faciliter la segmentation des coupes de manguier
/*
Installer loci bio formats
http://www.loci.wisc.edu/files/software/loci_tools.jar
Installer Colour Deconvolution
http://www.mecourse.com/landinig/software/colour_deconvolution.zip

*/

/*
NdpiMacroBatch --> Permet d exporter au format tif calibré les fichiers ndpi d un repertoire source dans un repertoire cible
	Option clic droit
		Numero de serie a choisir dans le plugin Bioformat Importer a une serie correspond une resolution d image a importer

Crop --> Pour detecter les objets d une taille superieur a taille_crop 
		dupliquer et sauver les images
	Option clic droit : taille du fitre, taille mini de l objet,  elargissement avant le crop		

New --> ouvre l image suivante dans le dossier (avec son Roi associe si il existe)
		sauve et remet a zero le Roi Manager 
		
Zonation --> Delimitation de l objet Total, 
	puis du bois 
	puis de ma moelle si la methode de seuillage pour la moelle n est pas bonne
	on peut choisir l autre (ou HSB ou niveaux de gris)
	
	Option clic droit --> taille du filtre
		Filtre Total : taille du filtre gaussien pour attenuer les details
		Filtre Bois : taille du filtre gaussien pour attenuer les details
		Filtre Moelle : taille du filtre gaussien pour attenuer les details
		Methode de seuillage de la moelle HSB ou Niveaux de gris

Xyleme --> Detection du Xyleme dans le bois
	Option clic droit 
		Taille mini d un vaisseaux 
	si changement remise a zero de tous les vaisseaux deja detectes
	
	
Sclerenchyme  --> Délimitation des tissus de soutien dans la zone externe
	
Resultats --> Resultats  et sauvegarde le roi manager
	clic droit permet d avoir les resultats sur tout un dossier

*/

macro "AutoRun"{
	setTool("rectangle");
	setForegroundColor(255, 255, 255);//premier plan blanc
	setBackgroundColor(0,0,0);// fond noir
	run("Set Measurements...", "area shape display redirect=None decimal=3");
	requires("1.49p");
}

/////////////////////variables glogales////////////////////////////
// ndpi batch
var numserie=2; //a verifier avec plugin-loci-bioformat importer

//crop
var taille_crop=10000;
var filtre_crop=50;
var enlarge_crop=10;
//zones
var seuil_total=newArray(0,164);
var seuil_bois=newArray(162,255);
var seuil_moelle_hsb=newArray(0,170);
var seuil_moelle_ndg=newArray(95,255);
var param_zone=newArray(10,10,15);
var methode="HSB";
var ok="Oui";

//xyl
var area_mini_xyl=150; //surface mini du xyleme 
//scl
var seuil_scl=newArray(0,86);
var param_scl=newArray(2,5000,2000000);// filtre, taille mini, taille mini

var title1 = "Resultats";
var title2 = "["+title1+"]";
var fe = title2;
	
////////////////////////////////////////////////////New Image//////////////////////////////////////////////////////////////////

macro "New Action Tool - C16aD9dDaeCec7D18D40D50Cca4D6bD6cD6dD6eCffbD71Da2C09eDbdCfeaD27D28D36D37D46D55Da4Ccb7D76D86CfffD00D01D02D03D04D05D06D07D08D09D0aD0bD0cD0dD0eD0fD9fDd0Dd1Dd2Dd3Dd4Dd5DdfDe0De1De2De3De4De5De6De7De8De9DeaDeeDefDf0Df1Df2Df3Df4Df5Df6Df7Df8Df9DfaDfdDfeDffC368D8cCed9D10D20Ccb6D78CfedD2fC4beDadCffaD25D44D53Cdc8D74D84C28bDa6Db6Dc6Dd7Dd8Dd9DdaDdbDebDfbCed8D31Cdb5D66Dc4CffcD81D91Da1C1afDaaDb9DbaCfeaD42D45D94Db3Cec7D4dD5cCb93D7eD8eCfe9Da5Db5Cdb6D1eDa0CffeD8fC8ceDbeDcdDdcCffbD22D23D24D32D33D34D43D93Da3Db1Db2Cdc8D73C27bDceDddDecCed8D11D12D13D41Cca4D6aCffbD92C0afDacDbbCedbD3fDc0Cec7D1bD1cD70D80C368D7bCee9D2aD39D48D58Cdb6D2eD3eD4eD5eD62D63C5cfDc7Dc8Dc9DcaCed8D2dD3cD4bD5aCed8D2bD3aD3bD4aD59Cdb5D64Dc2C2afDa8CffaD26D35D54Cec7D19Cca5D79D89Cfe9D47D56D95Db4Cec6D1dD90CffeD1fC9bdDafDcfDedDfcCdc9D72C17bD97D98D99D9aD9bCec8D14D15D16D17D30Cca4D6fC09eDccCcc7D75C886D7cD8dCed9D49D52Ccb6D77D87C4bfD9cCed7D3dD4cD5bCdb5D65Dc3Cec7D1aD60Db0Cba4D7dCfe9D21D29D38Cdc8D83Cda5D67D68Dc5C0afDb8Cdc7D5dC69aD96Cfd9D51C3beDa7CfebD82C8aaDdeCdd9D61C17bDbfC787D9eCcb6D88Cee9D57Cda4D69C478D8bC2bfDa9Cec9D4fC09fDbcCdc7Dc1Cdb6D5fC5bfDcbCca5D7aD8aCed8D2cC1afDabC59bDd6C3bfDb7CddcD7fCdc7D85"{
	if (nImages==0){
		open();
		//info_genotype_etat();	
		
	}
	
	else{
	
	dir=getInfo("image.directory");
		nom=getTitle();
		nom=replace(nom,".tif","");
		roiManager("Save", dir+nom+"-RoiSet.zip");//sauvegarde de roi manager
		run("Remove Overlay");
		run("Save");	
	
	
	
	run("Open Next");
	if (isOpen("ROI Manager")){
		roiManager("Deselect");
		n = roiManager("count");
		if (n>0) roiManager("Delete");
	}
	
	etat="";

	dir=getInfo("image.directory");
	nom=getTitle();
	nom=replace(nom,".tif","");
	if (File.exists(dir+nom+"-RoiSet.zip") ) 	roiManager("Open",dir+nom+"-RoiSet.zip");
}

}





/////////////////////////////////////////////////Zonation///////////////////////////////////////////////////////////////////////////////

macro "Zonation Action Tool - C147D6aD6bD87D94CbccD0eCaabDefCcdfD15D16D26D27D36D37D43D46D47D56D57D66Db7Dc6Dc7Dd6Dd7De6De7C469D97CbdfD19D1aD2aD2bD3aD3bDcaDdaDdbDeaDebCabfD4bD6dD7dD8dD9dDadCdefD23D33Db2Dd3De3C358D86Da5CcdeD11CabdDf6Df7Df8CddfDc4C9abD2fCcdfD17D28D38D48D58Db8Dc8Dd8De8CacfDbaCeefD31D41D51D61D71D81D91Da1Db1Dc1C258D84D88D8bD9bDabCcccDffCabdDf9DfaDfbC8abD3fD4fD5fD6fD7fD8fD9fDafDbfDcfDdfCcdfD18D29D39D49Da8Da9Db9Dc9Dd9De9CacfD1dD2eD3eD4eD5dD5eD6eD7eD8eD9eDaeDbeDceDdeCdefD21D22D32D42Dc2Dd1Dd2De2C46aDa3CcdfD55Db6CacdDf4Df5CdefD12D13D14D24D25D34D35D45D52Dc3Dc5Dd4Dd5De4De5C9bdD1eDfcCacfD1cD2dD3dD4dDcdDddDedC248D78D79D7bD96Da4CbceD20D30D40D50D60D70D80D90Da0Db0Dc0Dd0CabdD04D05D06D07D08D09D0aC67aD89CacfD4cDbdC369D63D6cD73D7cD83D8cD93D9cDacCbcfD1bD2cD3cD4aDdcDecCabeD76C9bdDb3DfdCbcfD99DcbC258D7aCdddD0fCbcdD03C79cD68CcdfD62D72D82D92Da2De1CaceDa7C9beDeeC148D95CbcdD02CabcD0dC57bD5cC369D69CbdfD67C8acD98CccdDe0Df1CcdfD44CbcdDf2Df3C68bDbcCacfDccCabeD59CdddDf0C89cD8aCbceD65D75CbbbD1fC57aD54D77C359D5bD64D74CccdD01C68bD85DbbC9beD9aDaaCcddD10CabdD0bD0cC58bD53D5aC78bDa6CdddD00CabdDb5CabcDfeC79bDb4"{
	origid=getImageID();
	//info_genotype_etat();
	run("Select None");
	
	//Total
	run("Duplicate...", "title=nb");
	run("8-bit");
	run("Gaussian Blur...", "sigma="+param_zone[0]);
	Validation_surface("Total",seuil_total);
		
	//Bois
	selectImage(origid);
	run("Select None");
	run("Duplicate...", "title=Bois");
	run("Gaussian Blur...", "sigma="+param_zone[1]);
	run("HSB Stack");
		run("Next Slice [>]");
		run("Delete Slice");
		run("Next Slice [>]");
		run("Delete Slice");
		Validation_surface("Bois",seuil_bois);
		
	//Moelle
	ok="Oui";
	if (methode=="HSB") {
		moelle_hsb();
		valid_methode();		
		}
	if (ok=="Oui"){
		if(methode=="Niveaux de gris"){
			moelle_ndg();
			valid_methode();		
		}
	}
	
	ok="Oui";
	selectImage(origid);
	roiManager("Deselect");
	roiManager("Show All");
	//setTool("rectangle");
	
	}


macro 'Zonation Action Tool Options'  {
    //on right-click, the corresponding 'Options' macro is run
    items=newArray("HSB","Niveaux de gris");
    Dialog.create("Zonation");
    Dialog.addMessage("Tailles des filtres");
    Dialog.addNumber("Filtre Total:", param_zone[0]);
    Dialog.addNumber("Filtre Bois:", param_zone[1]);
	Dialog.addNumber("Filtre Moelle:", param_zone[2]);
	Dialog.addChoice("Methode de seuillage :", items, methode);
    Dialog.show();
    param_zone[0]=Dialog.getNumber();
    param_zone[1]=Dialog.getNumber();;
	param_zone[2]=Dialog.getNumber();;;
	methode=Dialog.getChoice();
      
  	}

	
//////////////////////////////////Xyleme///////////////////////////////////////////////////////////////////

macro "Xyleme Action Tool - C147D5bD65D69D77D78D87D88D95CbbbDffC9acDb4CcdfD16D17D26D27D36D37D46D47D83Db6Db7Dc6Dc7Dd6Dd7De6De7C46aD85CbcfD1cD2cD7cD8cDdcDecCacfD4dD5dDadDbdCdefD11D13D23Dc2Dd3De1De3C258Da4CbceDa7Db2Db5CabeD08DccDf8CddfDc4C8acD1fDefCcdfD19D29D39Dc9Dd9De9CacfD1dD2dD2eD3dD3eD4eD5eD6dD6eD7dD7eD8dD8eD9dD9eDaeDbeDcdDceDddDdeDedCeefD21D31D41D51D61D71D81D91Da1Db1Dc1Dd1C248D54D66D99D9aCbceD02D20D30D40D50D60D70D80D90Da0Db0Dc0Dd0Df2C9beD0cDfcDfdC68aDb3CbdfD1aD1bD2aD2bD3aDb9DcaDdaDdbDeaDebCacfD3bCdefD12D22D72D82Dd2De2C369D67CbdfD45CaceD03D04D05D7bDf3Df4Df5CdefD14D15D24D25D32D34D35Dc5Dd4Dd5De4De5C8beD2fD3fD4fD5fD6fD7fD8fD9fDafDbfDcfDdfCcdfD18D28D38D48Db8Dc8Dd8De8C148D76D86DabCbcdD01D10De0Df1C9beD0dD58C57aD75Da5CbcfD49C358DaaCbcfD57DcbCaceD06D07Df6Df7C8adD4aD56C258D53D5aDa3C9beD09D0aD0bD3cD8bDf9DfaDfbC68bDa9CacfD1eDeeC479D97CcdfD62D73Dc3CcddD00Df0CabdD74C47aD4cC258D64D68D6aD89D96C68bD63C369DacCcdfD92CbceD42CabeDa8C57aDbcC359D98D9bC8adD6cC79cD44C46aD43D4bD5cCbbcD0fC9adD0eDfeC47aD6bC258D94C58bD93C369D79C79cD9cC9bdD84DbaC47aD59C68bD8aC469D55CcdfD33CabeD52Da2C57bDbbC89cD7aC9acDa6"{


	ncount=roiManager("count");
	for(i=ncount-1;i>=0;i--){
		roiManager("Select", i);
		nom_item=call("ij.plugin.frame.RoiManager.getName", i);
		if (startsWith(nom_item, "Xyl")) roiManager("Delete");
		}
	origid=getImageID();
	ncount=roiManager("count");
	run("Select None");
	run("Duplicate...", "title=xyl");
	run("Split Channels");
	selectWindow("xyl (blue)");
	close();
	selectWindow("xyl (red)");
	close();
	selectWindow("xyl (green)");
	rename("xyl");
	setBackgroundColor(0, 0, 0);
	total_fait=0;
	for(i=0;i<ncount;i++){
			roiManager("Select", i);
			nom_item=call("ij.plugin.frame.RoiManager.getName", i);
			
						if (nom_item=="Total"){
							total_fait=1;
													}
						if (nom_item=="Bois"){
							run("Clear Outside");
							}
						if (nom_item=="Moelle"){
							run("Clear", "slice");
							}
					}
		if (total_fait==0){
			close();
			 exit("Zones a determiner avant");
			} 		
		roiManager("Deselect");
		run("Select None");
		setAutoThreshold("Default dark");
		n_avant=roiManager("count");
		run("Analyze Particles...", "size="+area_mini_xyl+"-Infinity add");
		close();
		n_apres=roiManager("count");
		for (i=n_avant; i<n_apres; i++) {	
		roiManager("Select", i);
		roiManager("Rename", "Xyl");
		
		}		
							
		selectImage(origid);
		roiManager("Deselect");
		roiManager("Show All");	
	
}
/////////////////////
macro 'Xyleme Action Tool Options'  {
	Dialog.create("Xyleme"); //e.g., put a dialog here
    Dialog.addNumber("Surface minilale :", area_mini_xyl);
    Dialog.show();
    area_mini_xyl = Dialog.getNumber();

		
	}
	
//////////////////////////////////Sclerenchyme///////////////////////////////////////////////////////////////////

macro "Sclerenchyme Action Tool - C147D64D66D87D98CbbcDffCabcD0eCcdfD16D17D26D27D36D37D47D95Db5Db6Dc6Dc7Dd6Dd7De6De7C68bD75CcdfD19D29D39D46Dc9Dd9De9CacfDacDeeCdefD12D13D23D33D52Db2Dc3Dd3De1De3C359D55D5bD63CbdfD1aD1bD2aD2bD3aD3bD49D69D79DcaDcbDdaDdbDeaDebCabeDf8CdefD14D15D24D25D34D35D43Dc4Dc5Dd4Dd5De4De5C7adD7aCcdfD18D28D38D48D58Da6Dc8Dd8De8CacfD1dD2dD3dD4dD5dD6dD9dDadDbdDcdDddDedCeefD21D31D41D51D61D71D91Da1Db1Dc1Dd1C258D99CbcfD1cD2cD3cD4cDbcDccDdcDecC9beD4aC9abDefCacfD2eD3eD4eD5eD6eD7dD7eD8dD8eD9eDaeDbeDceDdeCdefD22D32D42D81Dc2Dd2De2C579D74CbceD30D40D50D60D62D70D80D90Da0Db0Dc0Dd0Df2C9adD89C248D65D77D7bD83D93D9aDa9CbcfD45D59D68CabdD07D08D09D0aC8acD2fD3fD4fD5fD6fD72D7fD8fD9fDafDbfDcfDdfCacfDbbC469D54D67CaceDf3Df4Df5C9acDfeC258D97D9bDa8DaaCcdfDb3CacdD03D04C9abD1fC58bD8aC9beDfcC148D6bD73D8bCbceD20C9bdD0bD0cD96Db8C79cD82C369D88CaceDf6Df7C8adDbaC258D76C9beD1eD4bDf9DfaDfbC57bD7cC9beD5cDfdCcdfD44D85Da2Da5Db4Db7CabdD05D06C46aD5aC9bdD53D92CddfD11CbcdD02C68bD8cD94Da4DabCbcdD01D10De0Df1C9bdD0dC68cD6cC57aD86CabeD57CdddD00C79cD78C68aDa3C47aD56CcccD0fC68bD6aC89cD84Db9C79cDa7C89cD9cCcddDf0"{
	ncount=roiManager("count");
	for(i=ncount-1;i>=0;i--){
		roiManager("Select", i);
		nom_item=call("ij.plugin.frame.RoiManager.getName", i);
		if (startsWith(nom_item, "Scl")) roiManager("Delete");
		}
	origid=getImageID();
	getPixelSize(unit, pixelWidth, pixelHeight);
	ncount=roiManager("count");
	run("Select None");
	run("Duplicate...", "title=scl");
	
	run("Colour Deconvolution", "vectors=[FastRed FastBlue DAB]");
	selectWindow("scl-(Colour_3)");
	close();
	selectWindow("scl-(Colour_2)");
	close();
	selectWindow("scl");
	close();
	selectWindow("Colour Deconvolution");
	close();
	selectWindow("scl-(Colour_1)");
	rename("scl");
	run("Set Scale...", "distance=1 known="+pixelWidth+" pixel=1.000 unit="+unit);
	setBackgroundColor(255, 255, 255);
	run("Gaussian Blur...", "sigma="+param_scl[0]);
	total_fait=0;
	for(i=0;i<ncount;i++){
			roiManager("Select", i);
			nom_item=call("ij.plugin.frame.RoiManager.getName", i);
			
						if (nom_item=="Total"){
							total_fait=1;
							run("Enlarge...", "enlarge=-"+param_zone[0]);
							run("Clear Outside");
													}
						if (nom_item=="Bois"){
							run("Clear", "slice");
							}
						}
		if (total_fait==0){
			close();
			 exit("Zones a determiner avant");
			} 		
		roiManager("Deselect");
		run("Select None");
		setThreshold(seuil_scl[0], seuil_scl[1]);
		title = "Sclerenchyme";
		msg = "Regler le seuil";
		waitForUser(title, msg);
		getThreshold(lower, upper);
		seuil_scl[0]=lower;
		seuil_scl[1]=upper;		
		run("Analyze Particles...", "size="+param_scl[1]+"-"+param_scl[2]+" show=Masks exclude");
		run("Create Selection");
		RoiAdd("Scl");
		close();
		selectWindow("scl");
		close();
		setBackgroundColor(0, 0, 0);					
		selectImage(origid);
		roiManager("Deselect");
		roiManager("Show All");	
	
}
/////////////////////
macro 'Sclerenchyme Action Tool Options'  {
	Dialog.create("Sclerenchyme");
    Dialog.addMessage("Parametres");
    Dialog.addNumber("Taille du filtre:", param_scl[0]);
    Dialog.addNumber("Taille mini:", param_scl[1]);
	Dialog.addNumber("Taille maxi:", param_scl[2]);
	Dialog.show();
    param_scl[0]=Dialog.getNumber();
    param_scl[1]=Dialog.getNumber();;
	param_scl[2]=Dialog.getNumber();;;
	

		
	}
		





/////////////////////////////////////////////////////Resultats/////////////////////////////////////////////////////////////////////////////////////
macro "Resultats Action Tool - C052DbfDcfDdfDebDecDedDeeDefCbebD5dDc6C364D84D85D93D94D95Da3CffeDa9C282D5bCdecD7aC798D83CfffD05D06D07D08D09D0aD0bD0cD0dD0eD1eD2eD3eD41D4eD51D5eD61D6eD71D73D7eD81D8eD91D97D9eDa1Da7Da8DaeDb1Db9DbaDbeDc1DcbDccDceDd1Dd2Dd3Dd4Dd5Dd6Dd7Dd8Dd9DdaDdbDddDdeC063D59CbceDf3Df4Df5Df6C7b4D65D9aCfffDa6Db4Db5Dc3Dc4DdcC592D4cCefeD12D14D32D63CacbD01C052DafCdedD16D17D18D19D1aD1bD1cD1dD23D24D25D26D27D28D35D36D52D62D72D82D92Da2Db2Dc2C385D7cCfffD13D22Db6DcaC382D4bD5cCdeeD00C9daDbdC163D5fDe5De6CbceDf0Df1Df2C8c5D56C6a1D3cCffeD04D31D8cCbceDf7Df8Df9CdddD74C484D75C364D86CdedD15D42Dc5C8c8D57C163D6fC8c4D67D89C493D33CacdDfaDfbDfcDfdDfeDffC172D5aC7a4D87C483D90Da0CeeeDa5CadaD3aC164D58C9c5D99C5a4D53CcecD2aD2bD2dD38D39D46D47Db8C273D1fDe1C798Db3C163D7fDe7De8C8b4D76C493D70CbebDb7C053D69C6a5D30C383Dd0CadaD9dC273D3fD4fDe3De4C9d4D78C5a3D44CcedD29D37C576Da4C373D0fDe0Cac8DaaC173D6aC9c4D77D88C593D50C272D6cC8b7D02D11C483D80CaeaD8dDc9C9c7D2cC7b4D55D9bCcdcD03D21C273D2fDe2C8b8D20C063D8fDe9C592D3bCaebD7dDc8C062D9fDeaC496D7bCadaDadDcdC593D68C9c7D64C493D43C172D4aD6bC6b5DbcCbdaD34C8c6D9cC5a4DacC8a9D96C8c4D66D8aC493D60CbebD6dDc7C5a6D49C383Db0Dc0C9c4D79C6a3D54DabCad9D8bC593D40C9c7D45CcebD98CacbD10CcecD4dCcdbDbbCcecD3dD48"{

		dir=getInfo("image.directory");
		nom=getTitle();
		nom=replace(nom,".tif","");
		mesure_r();
		roiManager("Save", dir+nom+"-RoiSet.zip");//sauvegarde de roi manager
		run("Remove Overlay");
		//run("Save");		
}

macro 'Resultats_R Action Tool Options'  {

		dir = getDirectory("Choisir un repertoire source");
		list = getFileList(dir);
		setBatchMode(true);
		for (i=0; i<list.length; i++) {												
			 if (endsWith(list[i], ".zip")){	
			 				roiManager("Open", dir+list[i]);										//condition sur l extension tif
			 			 nom=substring(list[i],0,indexOf(list[i],"-"));		//nom sans l extension
			 			open(dir + nom+".tif");	
			 				mesure_r();
			 				close();
			 				roiManager("Reset");
			}

}
}




///////////////////////////////////////////////////????????????????????????????????????????????????????

macro "About These Macros Action Tool - C000C111C222C333C444C555D54D55D63D73D7cD7dD83D88D89D8aD8cD8dD93D97Da3Da4Da5Da6C555D7aDb5C555D72D82C555D45Da7C555D98C555D53C555D79C555C666Db4C666Db6C666C777D92C777D87C777D64C777D62C777C888D44C888D96C888D78C999D94C999CaaaCbbbD99Db3CbbbDa2CbbbDa8CbbbCcccDb7CcccD52CcccD43CcccD65CcccCdddD74CdddD77CdddD86CdddD84D95CdddD00D01D02D03D04D05D06D07D08D09D0aD0bD0cD0dD0eD0fD10D11D12D13D14D15D16D17D18D19D1aD1bD1cD1dD1eD1fD20D21D22D23D24D25D26D27D28D29D2aD2bD2cD2dD2eD2fD30D31D32D33D34D35D36D37D38D39D3aD3bD3cD3dD3eD3fD40D41D42D46D47D48D49D4aD4bD4cD4dD4eD4fD50D51D56D57D58D59D5aD5bD5cD5dD5eD5fD60D61D66D67D68D69D6aD6bD6cD6dD6eD6fD70D71D75D76D7bD7eD7fD80D81D85D8bD8eD8fD90D91D9aD9bD9cD9dD9eD9fDa0Da1Da9DaaDabDacDadDaeDafDb0Db1Db2Db8Db9DbaDbbDbcDbdDbeDbfDc0Dc1Dc2Dc3Dc4Dc5Dc6Dc7Dc8Dc9DcaDcbDccDcdDceDcfDd0Dd1Dd2Dd3Dd4Dd5Dd6Dd7Dd8Dd9DdaDdbDdcDddDdeDdfDe0De1De2De3De4De5De6De7De8De9DeaDebDecDedDeeDefDf0Df1Df2Df3Df4Df5Df6Df7Df8Df9DfaDfbDfcDfdDfeDff"{
open(getDirectory("macros")+"toolsets\\PHIV_Mango_toolset.txt");
}








	
	/////////////////////////////////////////////////////FUNCTIONS///////////////////////////////////////////////////
var function RoiAdd(nom) {
	roiManager("Add");
	n = roiManager("count");
	roiManager("Select", n-1);	
	roiManager("Rename", nom);
}

	
	var function Seuil(seuils){
	setThreshold(seuils[0], seuils[1]);
	run("Convert to Mask");
}


function Validation_surface(zone,seuils){
	run("Threshold...");
	setThreshold(seuils[0], seuils[1]);
	run("Threshold...");	
	setTool("wand");
	title = zone;
	msg = "Regler le seuil puis selectionner avec la baguette";
	waitForUser(title, msg);
	getThreshold(lower, upper);
	seuils[0]=lower;
	seuils[1]=upper;
	roiManager("Add");
	n = roiManager("count");
	roiManager("Select", n-1);
	roiManager("Rename", zone);
	close();
  selectImage(origid);
  roiManager("Select", n-1);
	setTool("brush");
	call("ij.gui.Toolbar.setBrushSize", 100); 
	waitForUser(zone,"Correction de la selection");
	//run("Interpolate", "interval=1");//voir feret!
	roiManager("Update");	
	roiManager("Deselect");
	run("Select None");	
	setTool("rectangle");
}

var function ChoixSeuils(titre,seuils){
	run("Threshold...");
	setThreshold(seuils[0], seuils[1]);
	run("Threshold...");
	waitForUser(titre,"Reglage du Seuil");
	getThreshold(lower, upper);
	seuils[0]=lower;
	seuils[1]=upper;
	}


var function raz(titre){
	if (roiManager("count")>0){
		roiManager("Deselect");
		roiManager("Delete");
		}	
	run("Remove Overlay");
	run("Clear Results");
	print ("\\Clear");
	}



function NA(valeur){
	if (valeur==-1)valeur="NA";
	return valeur;
}

function deux_decimal(valeur){
	if (valeur!=-1){
		valeur= round(valeur*100)/100;
	}
	else valeur="NA";
	return valeur;
}


function moelle_hsb(){
	selectImage(origid);
	run("Select None");
	run("Duplicate...", "title=Moelle");
	run("Gaussian Blur...", "sigma="+param_zone[2]);
	run("HSB Stack");
	run("Next Slice [>]");
	run("Delete Slice");
	run("Next Slice [>]");
	run("Delete Slice");
	Validation_surface("Moelle",seuil_moelle_hsb);
}

function moelle_ndg(){
	selectImage(origid);
	run("Select None");
	run("Duplicate...", "title=Moelle");
	run("8-bit");
	run("Gaussian Blur...", "sigma="+param_zone[2]);
	Validation_surface("Moelle",seuil_moelle_ndg);
}

function delete_moelle(){
	ncount=roiManager("count");
	roiManager("Select", ncount-1);
	roiManager("Delete");
}

function valid_methode(){
	roiManager("Show All");
	Dialog.create("Validation");
	items = newArray("Oui", "Non");
    Dialog.addMessage("Segmentation Moelle");
    Dialog.addRadioButtonGroup("Correcte", items, 2, 1, "Oui");
    Dialog.show();
    ok = Dialog.getRadioButton();
	if (ok=="Non"){
		delete_moelle();
		if (methode=="HSB")	{
			methode="Niveaux de gris";
			moelle_ndg();
		}
		else {
			methode="HSB";
			moelle_hsb();
		}		 
	}
}









function mesure_r(){
	area_total=-1;
	diam=-1;
	area_bois=-1;
	area_moelle=-1;
	nb_fx_xyl=0;
	area_xyl=-1;
	
	ncount=roiManager("count");
	for(i=0;i<ncount;i++){
			roiManager("Select", i);
			nom_item=call("ij.plugin.frame.RoiManager.getName", i);
			
						if (nom_item=="Total"){
							getStatistics(area);
							area_total=area;////////////////////////////////////////////area_total
							run("Set Measurements...", "area centroid feret's redirect=None decimal=3");
							List.setMeasurements;
							diam=List.getValue("Feret");////////////////diam
							}
						if (nom_item=="Bois"){
							getStatistics(area);
							area_bois=area;////////////////////////////////////////////surfzone1
							}
						if (nom_item=="Moelle"){
							getStatistics(area);
							area_moelle=area;////////////////////////////////////////////surfzone1
							}
						if (startsWith(nom_item, "Xyl")){
							nb_fx_xyl++;////////////////////////////////////////////nb fx xyl
							getStatistics(area);
							area_xyl=area_xyl+area;
						}
						if (nom_item=="Scl"){
							getStatistics(area);
							area_scl=area;////////////////////////////////////////////surfzone1
							}
		}	
		run("Select None");
		area_bois=area_bois-area_moelle;
		area_total=deux_decimal(area_total/1000000);
		area_bois=deux_decimal(area_bois/1000000);
		area_moelle=deux_decimal(area_moelle/1000000);
		area_xyl=deux_decimal(area_xyl/1000000);
		area_scl=deux_decimal(area_scl/1000000);
		diam=deux_decimal(diam/1000);
	

		if( !isOpen("Resultats")){
		run("Table...", "name="+title2+" width=1400 height=600");
print(fe, "\\Headings:Image \tdiametre \ttotale \tbois \tmoelle \tnb_xyl \tsurf_xyl \tsurf_scl ");

	}
print(fe, nom+"\t"+diam+"\t"+area_total+"\t"+area_bois+"\t"+area_moelle+"\t"+nb_fx_xyl+"\t"+area_xyl+"\t"+area_scl);

}	

		
