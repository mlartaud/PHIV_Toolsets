// PHIV Nikon Tool Set
// Marc LARTAUD MRI PHIV
// Boite a outils pour faciliter l export des fichiers NIS issu du nikon eclipse SE

// installer loci bio formats
//http://www.loci.wisc.edu/files/software/loci_tools.jar



/*
Outil 
*/

//variables glogales
var path="";
var NomImage="nom d image";
var numserie=2;
var Taille_pix=0;
var Taille_bar=100;
var ajout_bar=false;
var position_bar="Lower Right";
var color_bar="White";
var Taille_bar=100;
var	Epaisseur_bar=30;
var	Taille_font=100;
var Chemin="";

macro "AutoRun"{
	setTool("rectangle");
	setForegroundColor(255, 255, 255);//premier plan blanc
	setBackgroundColor(0,0,0);// fond noir
	run("Set Measurements...", "area shape display redirect=None decimal=3");
	requires("1.49p");
}


////////////////////////////////////////////////////////////////label=Nis////////////////////////////////////////////

macro "Nis_convert Action Tool - C332DeeC554D38DdaC653DabC655D63C443D1cD1fD2bD4eD4fD6fD7dD8fD9eDfbC664D58Ca92D18C666D44Dc3Dd3De3C443D0cC555D00D01D02D03D04D10D11D12D13D20D21D22D30D31D32D40D41D42D43D50D51D52D53D54D55D60D61D62D70D71D72D80D81D82D90D91D92Da0Da1Da2Db0Db1Db2Dc0Dc1Dc2Dd0Dd1Dd2Dd6De0De1De2Df0Df1Df2Df3C882DdbC665DdcC444D4bD7cDa8DcaDf7C885D24Cba3D88C887DfeC443DecC555D05C763D7bC665D14D23D33D56D57D73D83D93Da3Db3DbfC543D5eD8cC774D7aCa93Dd8C885Da5C444D06D07D08D39D5fDc9C992Df6C765D15D84C544D09D9bDefCa94D97Ccb2D5aCbbbDbcC333D0fD3aD3bD3cD3dD3eC663D0aD0bC764D16D79Cba2DaaC776Dc6C773D28C665D45D64Da4Da7Ca94D67Cba3D35De5C999DdeC443D0dD0eD2cD2dD2eD2fD3fD4cD4dD7eD7fD9cD9dD9fC873D17D1bDa9C553D1dD1eD5cD6cD6dD6eD8dD8eC875D74D94Cba2D8aC985D86C883DbbDcbC775Db4C554Db9Dd9De7Ca94D87Dd4Ccb3D95CeeeDcdDceDcfC333DacDaeDfdC553D2aC654De6Ca92D1aD68De8C883D6bD8bDc8DebC994Dc4Dd5De4Cbb3D75D96Db5C888DbeC773DbaDd7C874D98C993D47C885D85Da6C983D5bC775Df4Caa4Dc5Cdc2D25CcccDffC663D29D4aDf8C774D9aCaa2D19D48DeaC873D99DfaCba3D76CaaaDbdC885D46Cba2D69D6aDb7C777DddC993D27C775D34D65Ca94D77Ccb3Db6C333DafDfcC553D5dC885D66Ccb2D26Ca93Dc7De9C983D49Df9Cba2D89C653Db8C874D78Ca93D59CdddDccCaaaDdfC983D37Ccb3D36C332DadDedCba3Df5"{
run("Close All");
path = File.openDialog("Choose a file");
pathsansext=substring(path,0,indexOf(path,".nd2"));
NomImage=File.getName(path);
run("Bio-Formats Macro Extensions");
Ext.setId(path);
Ext.getSeriesCount(seriesCount);
serie="";
for (s=1;s<=seriesCount;s++){
serie=serie+" series_"+s;
}
run("Bio-Formats Importer", "open=["+path+"] color_mode=Composite view=Hyperstack stack_order=XYCZT "+serie);
list_image=getList("image.titles");
for (s=0;s<list_image.length;s++){
	selectWindow(list_image[s]);
	Stack.getDimensions(width, height, channels, slices, frames);
	Stack.setChannel(1);
	run("Blue");
	Stack.setChannel(3);
	run("Red");
	//if(frames>1)run("Stack to RGB", "frames");
	//if (slices>1)run("Stack to RGB", "slices");
}


}



/////////////////////////////////////////////////////////////////////

macro "ome_to_tif Action Tool - C136D5dCfffD00D01D02D03D04D05D06D07D08D09D0aD0bD0cD0dD0eD0fD10D11D12D13D14D15D1bD1cD1dD1eD1fD20D21D22D23D24D25D2bD2cD2dD2eD2fD30D31D3fD40D41D47D48D49D4aD50D51D56D57D5aD60D61D65D6bD70D71D72D73D74D75D7cD7dD7eD7fD80D81D82D83D84D85D8cD8dD8eD8fD90D91D95D9bDa0Da1Da6Da7DaaDb0Db1Db7Db8Db9DbaDbfDc0Dc1Dc2DcfDd0Dd1Dd2Dd3Dd4Dd5DdbDdcDddDdeDdfDe0De1De2De3De4De5DebDecDedDeeDefDf0Df1Df2Df3Df4Df5Df6Df7Df8Df9DfaDfbDfcDfdDfeDffC8baDc3C358Dd9CfffD32C248D78D88CfffD1aD2aD35D36D39D3aD3eD46D4fD55D58D59D5fD64D6aD6eD6fD7bD8bD94D9aD9eD9fDa5Da8Da9DafDb6Dc5Dc6DcaDceDdaDeaC57aDd6C148D53CfbcD16C487Dc4C259D62C89bD3cC247D6dCbbcDe9Cc02D28C249D43D5cDd7C38cDbdC085Db3CeeeD45D4eD5eD7aD8aDaeDb5DbeDc9DcdC469De7C17bDadC9abD54C137Dd8C8ceD9cCc35D29C248D67D97C47bD4bC085Da3CecdD38C36aD42C36bD77D87C9abD63C247D44D4dD68D98CccdDc7Cc34D18C249D4cC5adDcbC286D92C8baD93C29dDacDbcCdabD19C237D79D89C9baDa4C458D34De8C25aD52C89bD33CbcdD66D96Ce13D27C58bD76C46aD3bC27cD9dCabcD69D99CacdD5bC57bD86C276Db4CfcdD37C3a8Db2C9acD6cCcceDe6Cd45D17C4adDbbC297Da2Ce67D26CbcdDc8C8bdDccCadeDabCeeeD3d"{


	origid=getImageID();
	Taille_pix=PhysicalSizeY();
	run("Set Scale...", "distance=1 known="+Taille_pix +" pixel=1 unit=µm");
	if(bitDepth() ==24) exit();
	nom=getTitle();	
	index_ext= indexOf(nom, ".");
	nom=substring(nom,0, index_ext-1);
	Stack.getDimensions(width, height, channels, slices, frames);
	planes=plane_count();
	//print (planes);
	tab_cannaux=newArray(parseInt(planes));
	tab_chemin=newArray(parseInt(planes));
	num_cannaux=0;
	for (i=0; i<planes; i++){
		tab_cannaux[i]=component_count(i+1);
		chemin_optique(i+1);
		tab_chemin[i]=Chemin;
		num_cannaux=num_cannaux+tab_cannaux[i];
	}
	//Array.show(tab_cannaux);
	//Array.show(tab_chemin);
	num_frame=slices/num_cannaux;
	if (num_frame>1){ 
		run("Stack to Hyperstack...", "order=xyczt(default) channels="+num_cannaux+" slices="+num_frame+" frames=1 display=Composite");
		Stack.getDimensions(width, height, channels, slices, frames);
		c=1;
		for (i=0;i<planes;i++){
		if (tab_cannaux[i]==3){
			Stack.setChannel(c); 
			run("Blue");
			Stack.setChannel(c+1); 
			run("Green");
			Stack.setChannel(c+2); 
			run("Red");
			c=c+3;
			}
		if (tab_cannaux[i]==1){
		Stack.setChannel(c); 
		//print (tab_chemin[i]);
			lut_apply(tab_chemin[i]);
			c++;
				}
		
			}
			run("Channels Tool...");
	}
	else if (planes==slices){
		run("Make Composite", "display=Color");
		for (i=0;i<planes;i++){
		Stack.setChannel(i+1); 
		setMetadata("Label", tab_chemin[i]);
		lut_apply(tab_chemin[i]);
		}	
		run("Channels Tool...");
	}
	else{
		c=1;
		for (i=0;i<planes;i++){
		if (tab_cannaux[i]==3){
			run("Duplicate...", "title=temp duplicate range="+c+"-"+c+2);
			//run("Duplicate...", "title="+nom+tab_chemin[i]+" duplicate range="+c+"-"+c+2);
			run("Reverse");
			run("Stack to RGB");
			rename(nom+tab_chemin[i]);
			selectWindow("temp");
			close();
			selectImage(origid);
			c=c+3;
			}
		if (tab_cannaux[i]==1){
			run("Duplicate...", "title="+nom+tab_chemin[i]+" duplicate range="+c);
			lut_apply(tab_chemin[i]);
			selectImage(origid);
			c++;
				}
		}
		close();
	}
	
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

macro "Scale Action Tool - C111D4bD4cD6bCfffD00D08D09D0aD0bD0cD0dD0eD0fD10D18D19D1aD1bD1cD1dD1eD1fD20D21D22D23D25D26D27D28D29D2aD2bD2cD2dD2eD2fD30D31D32D33D35D36D37D38D39D3aD3bD3cD3dD3eD3fD40D41D42D43D45D46D47D48D4eD4fD50D51D52D53D55D56D57D58D59D5aD5dD5eD5fD60D61D62D63D65D66D67D68D6dD6eD6fD70D71D72D73D75D76D77D78D79D7aD7bD7dD7eD7fD80D81D82D83D85D86D87D88D8dD8eD8fD90D91D92D93D95D96D97D98D9aD9bD9cD9dD9eD9fDa0Da1Da2Da3Da5Da6Da7Da8DadDaeDafDb0Db1Db2Db3Db5Db6Db7Db8DbbDbcDbdDbeDbfDc0Dc1Dc2Dc3Dc5Dc6Dc7Dc8DcdDceDcfDd0Dd1Dd2Dd3Dd5Dd6Dd7Dd8Dd9DdaDdbDdcDddDdeDdfDe0De8De9DeaDebDecDedDeeDefDf0Df8Df9DfaDfbDfcDfdDfeDffC222D8bDabC03fD02D03D04D05D06D12D13D14D15D16D24D34D44D54D64D74D84D94Da4Db4Dc4Dd4De4Df2Df3Df4Df5Df6C89fD01D07D11D17De2De3De5De6Df1Df7C222D4aD6aD8aDaaC555D99Db9C999D5cC444Da9C999D6cD8cDacDccCbcfDe1De7C333D49D69D89DcaDcbC666D4dDc9CfffD7cDbaCbbbD5b"{
	run("Remove Overlay");
	Taille_pix=PhysicalSizeY();
	run("Set Scale...", "distance=1 known="+Taille_pix +" pixel=1 unit=µm");
	run("Scale Bar...", "width="+Taille_bar+" height="+Epaisseur_bar+" font="+Taille_font+" color="+color_bar+" background=None location=["+position_bar+"] bold overlay");
}

macro 'Scale Action Tool Options'  {
    //on right-click, the corresponding 'Options' macro is run
    Dialog.create("Ajout de la barre"); //e.g., put a dialog here
    //Dialog.addCheckbox("Ajout de la barre", ajout_bar);
    items = newArray("Upper Left", "Upper Right", "Lower Left" ,"Lower Right" );
  	Dialog.addRadioButtonGroup("Position", items, 2, 2, position_bar);
  	items = newArray("Black", "White");
  	Dialog.addRadioButtonGroup("Position", items, 1, 2, color_bar);
	Dialog.addNumber("Taille de la barre", Taille_bar);
	Dialog.addNumber("Epaisseur de la barre", Epaisseur_bar);
	Dialog.addNumber("Taille de la police", Taille_font);
    Dialog.show();
    //ajout_bar = Dialog.getCheckbox();
    position_bar=Dialog.getRadioButton;
    color_bar=Dialog.getRadioButton;
	Taille_bar=Dialog.getNumber();
	Epaisseur_bar=Dialog.getNumber();
	Taille_font=Dialog.getNumber();
  	}




////////////////////////////////////////////////////////////////label=NdpiExportBatch////////////////////////////////////////////

macro "NisMacroBatch Action Tool - C2a3D47CbceD89CbbcDaaCfffD00D01D02D03D04D05D06D07D09D0aD0bD0cD0dD0eD0fD10D11D12D13D1cD1dD1eD1fD20D21D2dD2eD2fD30D3eD3fD40D4eD4fD50D5fD60D6fD7fD8fD9fDa0DafDb0DbeDbfDc0Dc1DceDcfDd0Dd1Dd2DddDdfDe0De1De2DeaDebDedDefDf0Df1Df2Df3Df4Df5Df6Df7Df8Df9DfaDfbDfcDfdDfeDffCa99Dc6CffeDc8CcccD81C877D7cCeeeD4dD61DcdDd3DdcDe7CbbdD7aCbbbD7bDd4CfffD3dCcceD93C3c1D46CeeeD8eCcbcD6dCaaaD95Dc4CfffDd8De3De9CccdD68C998D9cCeefD18Da1DdaCbbdD8aC9d9D25CfffD08DaeDd9De8CddeD3aD3cD5aD7eC876D96Da6CeeeD15D23CbbbD8dCaaaDcbDd6CfffD14D1bD22D2cD41D70D80D90D9eDc2CcecD24C888Dc5CcccD2aDa2CacaD62CfffD5eDb1DdeDeeCdddD28C6b5D55CeeeD6eCbcbD58CaabDb4CdddD16C7a7D63CeffD17D51CccdDb9CbcbD76CeeeDb2Db8DdbDe4C4a4D37CeeeD19D29D39D4aDc9CabcD94D9aCaa9D97CccdD2bD49D92C887D8cCbbdD6aCbbbDbcCfffD31DecCdceD4bC4c2D45CbbcD59D74Da8CaabDbaCdddD91C998Db5CbcdD84CbcaD33C6a6D66CcbbD9dCba9Da7CdddD5dD75D82Dd7De6C988D86CcccDc3CacaD38D52D67CdedD32C6b6D34CeefD5bCbcbD72CbabD7dCdddD1aC8c6D43D54CccdD83D88CbdbD42C3a3D56CcceD69CbbcD98Db3Caa9D87CcccDadC777DbbCbbdDa9CabbD73CcceD78C3b2D36C988D9bCbbbDccC888DabCbbbD77CdddD71DbdC6d4D44CcccDc7CaabDa4C8a8D48CccdDa3C6a6D26Caa9D8bC987Db6CcbdD4cD5cCbbbDcaCddeD6bC4c3D35CbbaDb7Ca99Da5CbbeD99CadaD27C6b6D57CbbcD3bD6cCaaaDacC888Dd5C7b6D53CcdbD64CabbD85CcdbD65CccfD79CddcDe5"{


dir1 = getDirectory("Choix du Repertoire Source ");
list = getFileList(dir1);
dir2 = getDirectory("Choix du Repertoire Cible ");	
      for (i=0; i<list.length; i++) {
          showProgress(i+1, list.length);
			path=dir1+list[i];
			pathsansext=substring(path,0,indexOf(path,".nd2"));
			NomImage=File.getName(path);
			NomImage=substring(NomImage,0,indexOf(NomImage,".nd2"));
			run("Bio-Formats Macro Extensions");
			Ext.setId(path);
			Ext.getSeriesCount(seriesCount);
			serie="";
			for (s=1;s<=seriesCount;s++){
			serie=serie+" series_"+s;
			}
			run("Bio-Formats Importer", "open=["+path+"] color_mode=Composite view=Hyperstack stack_order=XYCZT "+serie);
			list_image=getList("image.titles");
			for (s=0;s<list_image.length;s++){
				selectWindow(list_image[s]);
				Stack.getDimensions(width, height, channels, slices, frames);
				Stack.setChannel(1);
				run("Blue");
				Stack.setChannel(3);
				run("Red");
				//if(frames>1)run("Stack to RGB", "frames");
				//if (slices>1)run("Stack to RGB", "slices");
				saveAs("Tiff", dir2+NomImage+"_s"+s+1+".tif");
				close();
			}   		   
		   
		   
	   }

	}



macro "About These Macros Action Tool - C000C111C222C333C444C555D54D55D63D73D7cD7dD83D88D89D8aD8cD8dD93D97Da3Da4Da5Da6C555D7aDb5C555D72D82C555D45Da7C555D98C555D53C555D79C555C666Db4C666Db6C666C777D92C777D87C777D64C777D62C777C888D44C888D96C888D78C999D94C999CaaaCbbbD99Db3CbbbDa2CbbbDa8CbbbCcccDb7CcccD52CcccD43CcccD65CcccCdddD74CdddD77CdddD86CdddD84D95CdddD00D01D02D03D04D05D06D07D08D09D0aD0bD0cD0dD0eD0fD10D11D12D13D14D15D16D17D18D19D1aD1bD1cD1dD1eD1fD20D21D22D23D24D25D26D27D28D29D2aD2bD2cD2dD2eD2fD30D31D32D33D34D35D36D37D38D39D3aD3bD3cD3dD3eD3fD40D41D42D46D47D48D49D4aD4bD4cD4dD4eD4fD50D51D56D57D58D59D5aD5bD5cD5dD5eD5fD60D61D66D67D68D69D6aD6bD6cD6dD6eD6fD70D71D75D76D7bD7eD7fD80D81D85D8bD8eD8fD90D91D9aD9bD9cD9dD9eD9fDa0Da1Da9DaaDabDacDadDaeDafDb0Db1Db2Db8Db9DbaDbbDbcDbdDbeDbfDc0Dc1Dc2Dc3Dc4Dc5Dc6Dc7Dc8Dc9DcaDcbDccDcdDceDcfDd0Dd1Dd2Dd3Dd4Dd5Dd6Dd7Dd8Dd9DdaDdbDdcDddDdeDdfDe0De1De2De3De4De5De6De7De8De9DeaDebDecDedDeeDefDf0Df1Df2Df3Df4Df5Df6Df7Df8Df9DfaDfbDfcDfdDfeDff"{
open(getDirectory("macros")+"toolsets/PHIV_Nikon_toolset.txt");
}

	




function Hyper2RGB(){
	setBatchMode(true);
	titre=getTitle();
	setSlice(1);
	 setMetadata("Label", "r");
	setSlice(2);
	 setMetadata("Label", "g");
	setSlice(3);
	 setMetadata("Label", "b");
	run("Stack to Images");
	run("Merge Channels...", "c1=r c2=g c3=b");
	selectWindow("RGB");
	rename(titre);
	setBatchMode(false);
}

function Bio_meta(path){
	run("Bio-Formats Macro Extensions");
	Ext.setId(path);
	for (s=0; s<6; s++) {
	  Ext.setSeries(s);
	  Ext.getSizeX(sizeX);
	  SX[s]=sizeX;
	  Ext.getSizeY(sizeY);
	  SY[s]=sizeY;
	  }
	Ext.close();
}


function plane_count(){
	infos=getMetadata("Info");
	index_Dimensions= indexOf(infos, "Dimensions: ");
	//print(index_Dimensions);
	if(index_Dimensions!=-1){
		index_fin_Dimensions= indexOf(infos, "\n",index_Dimensions);
		//print(index_fin_Dimensions);
		dim=substring(infos, index_Dimensions, index_fin_Dimensions);
		//print(dim);
		index_intero= indexOf(dim, "?");
		if(index_intero!=-1){
			index_1p= indexOf(dim, "(",index_intero);
			index_2p= indexOf(dim, ")",index_intero);
			plane=substring(dim, index_1p+1, index_2p);
			//print (plane);
			return parseInt(plane);	
		}	
	}
	return 0;
}

function component_count(num){
	infos=getMetadata("Info");
	index_plane_num= indexOf(infos, "Plane #"+num);
	index_compo=indexOf(infos, "Component Count",index_plane_num);
	index_valeur=indexOf(infos, ":",index_compo);
	return parseInt(substring(infos, index_valeur+2, index_valeur+3));
}

function chemin_optique(num){
	infos=getMetadata("Info");
	index_plane_num= indexOf(infos, "Plane #"+num);
	index_dia=indexOf(infos, "Nikon Ni-E, Illuminator(Illuminator-DIA):",index_plane_num);
	index_dia=indexOf(infos, "Nikon Ni-E, Illuminator(Illuminator-DIA):",index_dia+1);
	index_dia=indexOf(infos, "O",index_dia);
	dia=substring(infos, index_dia, index_dia+2);
	if(dia=="On") {
		Chemin= "Champ clair";
		
	}
	else {
	index_filtre=indexOf(infos, "Nikon Ni-E, FilterChanger(Turret1):",index_plane_num);
	index_filtre=indexOf(infos, ":",index_filtre);
	index_1p= indexOf(infos, "(",index_filtre);
	index_2p= indexOf(infos, ")",index_filtre);
	filtre=substring(infos, index_1p+1, index_2p);
	Chemin= filtre;
	}
}

function lut_apply(condition){
		if(condition=="UV-2A") run("Blue");
		if(condition=="GFP-L") run("Green");
		if(condition=="B-2A") run("Cyan");
		if(condition=="TRITC") run("Yellow");
		if(condition=="Cy3") run("Red");
}


function PhysicalSizeY(){
	infos=split(getMetadata("Info"),"<");
	for (i=0; i<lengthOf(infos); i++){
		row=split(infos[i],"\" ");
		for (j=0; j<lengthOf(row); j++){
			if (startsWith(row[j], "PhysicalSizeY")) return row[j+1] ;
			}
		}
}

